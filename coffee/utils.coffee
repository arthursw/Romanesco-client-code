define [
	'jquery'
	'paper'
	# 'js-cookie'
], ($) ->

	g = {}
	window.g = g
	g.DajaxiceXMLHttpRequest = window.XMLHttpRequest
	window.XMLHttpRequest = window.RXMLHttpRequest
	paper.install(window)
	g.templatesJ = $("#templates")

	#\
	#|*|
	#|*|  IE-specific polyfill which enables the passage of arbitrary arguments to the
	#|*|  callback functions of JavaScript timers (HTML5 standard syntax).
	#|*|
	#|*|  https://developer.mozilla.org/en-US/docs/DOM/window.setInterval
	#|*|
	#|*|  Syntax:
	#|*|  var timeoutID = window.setTimeout(func, delay, [param1, param2, ...]);
	#|*|  var timeoutID = window.setTimeout(code, delay);
	#|*|  var intervalID = window.setInterval(func, delay[, param1, param2, ...]);
	#|*|  var intervalID = window.setInterval(code, delay);
	#|*|
	#\
	if document.all and not window.setTimeout.isPolyfill
		__nativeST__ = window.setTimeout
		window.setTimeout = (vCallback, nDelay) -> #, argumentToPass1, argumentToPass2, etc.
			aArgs = Array::slice.call(arguments, 2)
			__nativeST__ (if vCallback instanceof Function then ->
				vCallback.apply null, aArgs
			else vCallback), nDelay

		window.setTimeout.isPolyfill = true
	if document.all and not window.setInterval.isPolyfill
		__nativeSI__ = window.setInterval
		window.setInterval = (vCallback, nDelay) -> #, argumentToPass1, argumentToPass2, etc.
			aArgs = Array::slice.call(arguments, 2)
			__nativeSI__ (if vCallback instanceof Function then ->
				vCallback.apply null, aArgs
			else vCallback), nDelay

	# $.ajaxSetup(
	# 	beforeSend: (xhr, settings)->
	# 		if (!/^(GET|HEAD|OPTIONS|TRACE)$/.test(settings.type) && !this.crossDomain)
	# 			xhr.setRequestHeader("X-CSRFToken", Cookies.get('csrftoken'))
	# )

	window.setInterval.isPolyfill = true

	g.specialKeys = {
		8: 'backspace',
		9: 'tab',
		13: 'enter',
		16: 'shift',
		17: 'control',
		18: 'option',
		19: 'pause',
		20: 'caps-lock',
		27: 'escape',
		32: 'space',
		35: 'end',
		36: 'home',
		37: 'left',
		38: 'up',
		39: 'right',
		40: 'down',
		46: 'delete',
		91: 'command',
		93: 'command',
		224: 'command'
	}

	g.getParentPrototype = (object, ParentClass)->
		prototype = object.constructor.prototype
		while prototype != ParentClass.prototype
			prototype = prototype.constructor.__super__
		return prototype

	# @return [Number] sign of *x* (+1 or -1)
	g.sign = (x) ->
		(if typeof x is "number" then (if x then (if x < 0 then -1 else 1) else (if x is x then 0 else NaN)) else NaN)

	# @return [Number] *value* clamped with *min* and *max* ( so that min <= value <= max )
	g.clamp = (min, value, max)->
		return Math.min(Math.max(value, min), max)

	g.random = (min, max)->
		return min + Math.random()*(max-min)

	# removes *itemToRemove* from array
	# problem with array.splice(array.indexOf(item),1) :
	# removes the last element if item is not in array
	Array.prototype.remove = (itemToRemove) ->
		if not Array.prototype.isPrototypeOf(this) then return
		i = this.indexOf(itemToRemove)
		if i>=0 then this.splice(i, 1)
		# for item,i in this
		# 	if item is itemToRemove
		# 		this.splice(i,1)
		# 		break
		return

	# @return [Array item] first element of the array
	Array.prototype.first = () ->
		return this[0]

	# @return [Array item] last element of the array
	Array.prototype.last = () ->
		return this[this.length-1]

	# @return [Array item] random element of the array
	Array.prototype.random = () ->
		return this[Math.floor(Math.random()*this.length)]

	# @return [Array item] maximum
	Array.prototype.max = () ->
		max = this[0]
		for item in this
			if item>max then max = item
		return max

	# @return [Array item] minimum
	Array.prototype.min = () ->
		min = this[0]
		for item in this
			if item<min then min = item
		return min

	# @return [Array item] maximum
	Array.prototype.maxc = (biggerThan) ->
		max = this[0]
		for item in this
			if biggerThan(item,max) then max = item
		return max

	# @return [Array item] minimum
	Array.prototype.minc = (smallerThan) ->
		min = this[0]
		for item in this
			if smallerThan(item,min) then min = item
		return min

	# check if array is array
	Array.isArray ?= (array)->
		return array.constructor == Array

	g.isArray = (array)->
		return array.constructor == Array

	g.isNumber = (n)->
		return not isNaN(n) and isFinite(n)

	# previously Array.prototype.pushIfAbsent, but there seem to be a colision with jQuery...
	# push if array does not contain item
	g.pushIfAbsent = (array, item) ->
		if array.indexOf(item)<0 then array.push(item)
		return

	g.deferredExecutionCallbackWrapper = (callback, id, args, oThis)->
		console.log "deferredExecutionCallbackWrapper: " + id
		delete g.updateTimeout[id]
		if not args? then callback?() else callback?.apply(oThis, args)
		return

	# Execute *callback* after *n* milliseconds, reset the delay timer at each call
	# @param [function] callback function
	# @param [Anything] a unique id (usually the id or pk of RItems) to avoid collisions between deferred executions
	# @param [Number] delay before *callback* is called
	g.deferredExecution = (callback, id, n=500, args, oThis) ->
		if not id? then return
		# id ?= callback.name # for ECMAScript 6
		# console.log "deferredExecution: " + id + ", updateTimeout[id]: " + g.updateTimeout[id]
		if g.updateTimeout[id]? then clearTimeout(g.updateTimeout[id])
		console.log "deferred execution: " + id + ', ' + g.updateTimeout[id]
		g.updateTimeout[id] = setTimeout(g.deferredExecutionCallbackWrapper, n, callback, id, args, oThis)
		return

	# Execute *callback* at next animation frame
	# @param [function] callback function
	# @param [Anything] a unique id (usually the id or pk of RItems) to avoid collisions between deferred executions
	g.callNextFrame = (callback, id, args) ->
		id ?= callback
		callbackWrapper = ()->
			delete g.requestedCallbacks[id]
			if not args? then callback() else callback.apply(window, args)
			return
		g.requestedCallbacks[id] ?= window.requestAnimationFrame(callbackWrapper)
		return

	g.cancelCallNextFrame = (idToCancel)->
		window.cancelAnimationFrame(g.requestedCallbacks[idToCancel])
		delete g.requestedCallbacks[idToCancel]
		return

	sqrtTwoPi = Math.sqrt(2*Math.PI)

	# @param [Number] mean: expected value
	# @param [Number] sigma: standard deviation
	# @param [Number] x: parameter
	# @return [Number] value (at *x*) of the gaussian of expected value *mean* and standard deviation *sigma*
	g.gaussian = (mean, sigma, x)->
		expf = -((x-mean)*(x-mean)/(2*sigma*sigma))
		return ( 1.0/(sigma*sqrtTwoPi) ) * Math.exp(expf)

	# check if an object has no property
	# @param map [Object] the object to test
	# @return true if there is no property, false otherwise (provided that no library overloads Object)
	g.isEmpty = (map)->
		for key, value of map
			if map.hasOwnProperty(key)
				return false
		return true

	# returns a linear interpolation of *v1* and *v2* according to *f*
	# @param v1 [Number] the first value
	# @param v2 [Number] the second value
	# @param f [Number] the parameter (between v1 and v2 ; f==0 returns v1 ; f==0.25 returns 0.75*v1+0.25*v2 ; f==0.5 returns (v1+v2)/2 ; f==1 returns v2)
	# @return a linear interpolation of *v1* and *v2* according to *f*
	g.linearInterpolation = (v1, v2, f)->
		return v1 * (1-f) + v2 * f

	g.ajax = (url, callback, type="GET")->
		xmlhttp = new RXMLHttpRequest()
		xmlhttp.onreadystatechange = ()->
			if xmlhttp.readyState == 4 and xmlhttp.status == 200
				callback()
			return
		xmlhttp.open(type, url, true)
		xmlhttp.send()
		return xmlhttp.onreadystatechange

	return g: ()-> return g